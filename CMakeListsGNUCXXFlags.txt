# Note: GCC makes one add extra flags
if( STRICT_BUILD )
  # Does not work with Sobogus: -Wsign-conversion -Weffc++ -Wold-style-cast
  # Does not work with Eigen: -Wvector-operation-performance -Wzero-as-null-pointer-constant -Wstrict-overflow=5
  # Does not work with SCISim: -Wfloat-equal -Wsuggest-final-types -Wsuggest-final-methods
  # Does not work with Qt5: -Wuseless-cast

  # Enable as many warnings as possible
  set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Werror -Wall -Wextra -Wpedantic -Wcast-align -Wdisabled-optimization -Wformat=2 -Winit-self" )
  set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Winvalid-pch -Wmissing-include-dirs -Wnoexcept -Woverloaded-virtual -Wsign-promo -Wstrict-null-sentinel" )
  set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wtrampolines -Wundef -Wconversion -Wctor-dtor-privacy -Wlogical-op -Wshadow" )
  set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wcast-qual -Wmissing-declarations -Wredundant-decls -Werror=double-promotion -Wlogical-op" )
  set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wunsafe-loop-optimizations -Wnull-dereference" )
endif()

# Disable run time type information
set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fno-rtti" )

# Enable extra optimizations; note that march=native seems to degrade performance on OS X
if( NOT APPLE )
  set( CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -march=native" )
endif()

# Flags for code coverage tests
set( CMAKE_CXX_FLAGS_COVERAGE "-O0 -g --coverage" CACHE STRING "Flags used by the compiler during coverage builds." FORCE )
mark_as_advanced( CMAKE_CXX_FLAGS_COVERAGE )

# Enable sanitizers, if requested
if( SANITIZER STREQUAL "address" )
  set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fsanitize=address" )
  # Workaround: explicity request gold linker
  if( NOT APPLE )
    set( CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -fuse-ld=gold" )
  endif()
elseif( SANITIZER STREQUAL "undefined" )
  # -fsanitize=nonnull-attribute causes issues with Eigen
  set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fsanitize=undefined -fno-sanitize-recover -fno-sanitize=nonnull-attribute" )
  # Workaround: explicity request gold linker
  if( NOT APPLE )
    set( CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -fuse-ld=gold" )
  endif()
elseif( NOT SANITIZER STREQUAL "none" )
  message( FATAL_ERROR "Error, only 'address' and 'undefined' are supported for GCC sanitizers, in the build system." )
endif()
